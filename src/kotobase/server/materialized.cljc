(ns kotobase.server.materialized
  "Storage-independent Datomic read semantics over materialized datom rows.

  This namespace is deliberately unaware of commit chains, object stores, or
  any particular IEngine implementation.  A storage adapter supplies the
  visible rows for one snapshot; this layer builds kotobase-peer's canonical
  in-memory DB and executes the shared q/pull/entity semantics.  Primary and
  candidate engines can therefore differ physically without reimplementing
  the public read contract."
  (:require #?(:clj  [clojure.edn :as edn]
               :cljs [cljs.reader :as edn])
            [arrangement.core :as arrangement]
            [kotobase-peer.core :as eng]))

(defn row-value
  "Decode one wire datom value.  Primary rows carry `:v_edn`; engine-native
  rows may carry `:v`.  Presence, rather than truthiness, distinguishes them
  so nil/false remain valid values."
  [row]
  (if (contains? row :v_edn)
    (arrangement/edn->link (edn/read-string (:v_edn row)))
    (:v row)))

(defn rows->db
  "Materialize current-state datom ROWS into kotobase-peer's canonical DB.

  Rows may be wire-shaped `{:e :a :v_edn :added}` or engine-shaped
  `{:e :a :v :added}`.  Retraction rows are applied in input order; callers
  providing a current snapshot normally provide assertions only."
  [rows]
  (eng/transact
   (eng/empty-db)
   (mapv (fn [{:keys [e a added] :as row}]
           [(if (= false added) :db/retract :db/add)
            e a (row-value row)])
         rows)))

(defn query-db
  "Execute either the legacy triple pattern or full Datalog query against DB."
  ([db query visible?] (query-db db query visible? nil))
  ([db query visible? inputs]
   (if (and (map? query)
            (or (contains? query :find) (contains? query :where)))
     (if (some? inputs)
       (eng/query db query visible? inputs)
       (eng/query db query visible?))
     (eng/q db query visible?))))

(defn query-rows
  "Materialize ROWS and execute `query-db`."
  ([rows query visible?] (query-rows rows query visible? nil))
  ([rows query visible? inputs]
   (query-db (rows->db rows) query visible? inputs)))

(defn redact-pulled
  "Apply the row visibility contract to every attr-keyed pull result map."
  [visible? value]
  (cond
    (map? value) (into {} (keep (fn [[k v]]
                                  (when (visible? {:a (str k)})
                                    [k (redact-pulled visible? v)]))
                                value))
    (set? value) (into #{} (map #(redact-pulled visible? %)) value)
    (sequential? value) (mapv #(redact-pulled visible? %) value)
    :else value))

(defn pull [db entity pattern visible?]
  (redact-pulled visible? (eng/pull db entity pattern)))

(defn pull-many [db entities pattern visible?]
  (mapv #(pull db % pattern visible?) entities))

(defn entity [db entity-id visible?]
  (redact-pulled visible? (eng/entity db entity-id)))

(defn entid [db ident]
  (eng/entid db ident))

(defn ident [db entity-id]
  (eng/ident db entity-id))
